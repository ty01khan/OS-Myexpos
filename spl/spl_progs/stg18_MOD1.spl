//Implementing Process Manager Module (Module-1)
alias functionNum R1;
alias currPID R2;

//Free User Area Page
if (functionNum == 2) then
	alias pageNum R3;
	pageNum = [PROCESS_TABLE + (currPID * 16) + 11];
	
	//Release pageNum
	multipush(R1, R2, R3);
	R1 = 2;
	R2 = pageNum;
	call MOD_2;
	multipop(R1, R2, R3);
	return;
endif;

//Exit Process
if (functionNum == 3) then
	//Invoking Free Page Table function
	multipush(R1, R2);
	R1 = 4;
	call MOD_1;
	multipop(R1, R2);

	//Invoking Free user Area Page function
	multipush(R1, R2);
	R1 = 2;
	call MOD_1;
	multipop(R1, R2);
	
	[PROCESS_TABLE + (currPID * 16) + 4] = TERMINATED;
	return;
endif;

//Free Page Table
if (functionNum == 4) then
	alias entry R3;
	alias pageNum R4;
	entry = 0;
	while (entry < 10) do
		pageNum = [PTBR + (2 * entry)];
		[PTBR + (2 * entry)] = -1;
		[PTBR + (2 * entry) + 1] = "0000";
		if (entry < 2) then
			entry = entry + 1;
			continue;
		endif;
		
		//Release Page
		multipush(R1, R2, R3, R4);
		R1 = 2;
		R2 = pageNum;
		call MOD_2;
		multipop(R1, R2, R3, R4);
		entry = entry + 1;
	endwhile;
	return;
endif;
